ext["mavenSigningKeyId"] = ''
ext["mavenSigningKeyPassword"] = ''
ext["mavenSigningKey"] = ''
ext["ossrhUsername"] = ''
ext["ossrhPassword"] = ''
ext["sonatypeStagingProfileId"] = ''
ext["sdkVersion"] = ''

File secretPropsFile = project.rootProject.file('local.properties')
if (secretPropsFile.exists()) {
    Properties p = new Properties()
    new FileInputStream(secretPropsFile).withCloseable { is -> p.load(is) }
    p.each { name, value -> ext[name] = value }
} else {
    // Use system environment variables
    ext["ossrhUsername"] = System.getenv('OSSRH_USERNAME')
    ext["ossrhPassword"] = System.getenv('OSSRH_PASSWORD')
    ext["sonatypeStagingProfileId"] = System.getenv('SONATYPE_STAGING_PROFILE_ID')
    ext["mavenSigningKeyId"] = System.getenv('MAVEN_SIGNING_KEY_ID')
    ext["mavenSigningKeyPassword"] = System.getenv('MAVEN_SIGNING_KEY_PASSWORD')
    ext["mavenSigningKey"] = System.getenv('MAVEN_SIGNING_KEY')
    ext["sdkVersion"] = System.getenv('SDK_RELEASE_VERSION')
}

nexusPublishing {
    repositories {
        sonatype {
            stagingProfileId = sonatypeStagingProfileId
            username = ossrhUsername
            password = ossrhPassword
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
        }
    }
}